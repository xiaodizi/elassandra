/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

import static org.elasticsearch.gradle.BuildPlugin.getJavaHome

apply plugin: 'elasticsearch.test-with-dependencies'

esplugin {
  description 'The Reindex module adds APIs to reindex from one index to another or update documents in place.'
  classname 'org.elasticsearch.index.reindex.ReindexPlugin'
  hasClientJar = true
}

integTestCluster {
  // Modules who's integration is explicitly tested in integration tests
  module project(':modules:parent-join')
  module project(':modules:lang-painless')
  // Whitelist reindexing from the local node so we can test reindex-from-remote.
  setting 'reindex.remote.whitelist', '127.0.0.1:*'
}

run {
  // Modules who's integration is explicitly tested in integration tests
  module project(':modules:parent-join')
  module project(':modules:lang-painless')
  // Whitelist reindexing from the local node so we can test reindex-from-remote.
  setting 'reindex.remote.whitelist', '127.0.0.1:*'
}

unitTest {
  /*
   * We have to disable setting the number of available processors as tests in the
   * same JVM randomize processors and will step on each other if we allow them to
   * set the number of available processors as it's set-once in Netty.
   */
  systemProperty 'es.set.netty.runtime.available.processors', 'false'
}

dependencies {
  compile "com.strapdata.elasticsearch.client:elasticsearch-rest-client:${version}"
  compile "com.strapdata.elasticsearch:elasticsearch-ssl-config:${version}"
  // for http - testing reindex from remote
  testCompile project(path: ':modules:transport-netty4', configuration: 'runtime')
  // for parent/child testing
  testCompile project(path: ':modules:parent-join', configuration: 'runtime')
}

thirdPartyAudit.ignoreMissingClasses (
  // Commons logging
  'javax.servlet.ServletContextEvent',
  'javax.servlet.ServletContextListener',
  'org.apache.avalon.framework.logger.Logger',
  'org.apache.log.Hierarchy',
  'org.apache.log.Logger',
)

forbiddenPatterns {
    // PKCS#12 file are not UTF-8
    exclude '**/*.p12'
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  logger.warn("Disabling reindex-from-old tests because we can't get the pid file on windows")
  integTestRunner.systemProperty "tests.fromOld", "false"
} else if (rootProject.rootDir.toString().contains(" ")) {
  logger.warn("Disabling reindex-from-old tests because Elasticsearch 1.7 won't start with spaces in the path")
  integTestRunner.systemProperty "tests.fromOld", "false"
} else {
  integTestRunner.systemProperty "tests.fromOld", "true"
  /* Set up tasks to unzip and run the old versions of ES before running the
   * integration tests. */
}
